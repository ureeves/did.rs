// DID scheme written in PEST style PEG. Translated to PEG from the ABNF rules
// defined in https://w3c-ccg.github.io/did-spec/#generic-did-syntax
path_abempty       = { "/"* }
pct_encoded        = { "%" ~ ASCII_HEX_DIGIT ~ ASCII_HEX_DIGIT }
unreserved         = { ASCII_ALPHA | ASCII_DIGIT | "-" | "." | "_" | "~" }
sub_delims         = { "!" | "$" | "&" | "'" | "(" | ")" | "*" | "+" | "," |
                       ";" | "="}

pchar              = { unreserved | pct_encoded | sub_delims | ":" | "@" }
query              = { ( pchar | "/" | "?")* }
fragment           = { ( pchar | "/" | "?")* }

method_char        = { ASCII_ALPHA_LOWER | ASCII_DIGIT }
method_name        = { method_char+ }
idchar             = { ASCII_ALPHA | ASCII_DIGIT | "." | "-" | "_" }
method_specific_id = { idchar* ~ (":" ~ idchar* )* }

param              = { param_name ~  ("=" ~ param_value)? }
param_name         = { param_char+ }
param_value        = { param_char* }
param_char         = { ASCII_ALPHA | ASCII_DIGIT | pct_encoded |
			           "." | "-" | "_" | ":" }


did                = { "did:" ~ method_name ~ ":" ~ method_specific_id }
did_url            = { did ~ (";" ~ param)* ~ path_abempty ~ ("?" ~ query)? ~
                       ("#" ~ fragment)? }
